buildscript {
	repositories {
		jcenter()
		mavenCentral()
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath "jp.classmethod.aws:gradle-aws-plugin:0.35"
		classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
	}
}

// basic plugins
plugins {
	id 'java'
	id 'maven'
	id 'maven-publish'
	// https://stackoverflow.com/questions/37555196
	// id "org.springframework.boot" version "2.1.2.RELEASE"
	id "io.spring.dependency-management" version "1.0.6.RELEASE"
}
apply plugin: 'org.springframework.boot'

// basic configuration
group = "jp.xet.spike"
ext.artifactId = "spring-spike"

// custom plugins
apply plugin: "jp.classmethod.aws.beanstalk"

// custom configuration
apply from: "${rootProject.projectDir}/gradle/version.gradle"
apply from: "${rootProject.projectDir}/gradle/sourceArtifact.gradle"

// compiler
sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(AbstractCompile) each {
	it.options.encoding = "UTF-8"
}

compileJava {
	options.compilerArgs << "-Werror"
	options.compilerArgs << "-Xlint:all" << "-Xlint:-processing" << "-Xlint:-deprecation"
}

// ======== libraries ========
repositories {
	jcenter()
	mavenCentral()
	maven { url 'https://dl.bintray.com/dai0304/maven' }
	maven { url 'https://jitpack.io' }
}

dependencies {
	// spring
	implementation "org.springframework.boot:spring-boot-starter"
	implementation "org.springframework.boot:spring-boot-starter-web"
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

	// aws
	// implementation 'jp.xet.spring.aws:aws-client-spring-boot-configuration:1.0.0'
	// implementation "com.amazonaws:aws-java-sdk-sts:$awsJavaSdkVersion"

	// others

	// lombok
	compile "org.projectlombok:lombok"
	annotationProcessor "org.projectlombok:lombok"
	testCompile "org.projectlombok:lombok"
	testAnnotationProcessor "org.projectlombok:lombok"

	// test
	testImplementation "org.springframework.boot:spring-boot-starter-test"

	// build
}

// ======== beanstalk bundle ========
task createDockerfile(type: Copy, dependsOn: jar) {
	from "src/main/bundle/Dockerfile"
	into "build/bundle"
	expand(jarFilename: jar.archiveName, artifactId: project.artifactId)
}

task createBundleResources(type: Copy, dependsOn: bootJar) {
	from "src/main/bundle"
	from jar.archivePath
	into "build/bundle"
	exclude "src/main/bundle/Dockerfile"
}

task createBundle(type: Zip, dependsOn: [createDockerfile, createBundleResources]) {
	archiveName = jar.archiveName.substring(0, jar.archiveName.length() - 4) + ".zip"
	from "build/bundle"
}

aws {
	region = "ap-northeast-1"
}

task uploadBundle(type: jp.classmethod.aws.gradle.elasticbeanstalk.AWSElasticBeanstalkUploadBundleTask, dependsOn: createBundle) {
	file project.createBundle.archivePath
	overwrite project.version.endsWith("-SNAPSHOT")
}

task awsEbCreateVersion(type: jp.classmethod.aws.gradle.elasticbeanstalk.AWSElasticBeanstalkCreateApplicationVersionTask, dependsOn: uploadBundle) {
	appName = "log-scatter"
	conventionMapping.bucketName = { uploadBundle.bucketName }
	conventionMapping.key = { uploadBundle.key }
	conventionMapping.versionLabel = { uploadBundle.versionLabel }
}


// ======== wrapper ========
wrapper {
	gradleVersion = '5.5'
	distributionType = Wrapper.DistributionType.ALL
}
